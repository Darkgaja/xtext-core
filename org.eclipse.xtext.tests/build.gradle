apply from: "${rootDir}/gradle/mwe2-workflows.gradle"

dependencies {
	api project(':org.eclipse.xtext.testing')
	api project(':org.eclipse.xtext.xtext.generator')
	api project(':org.eclipse.xtext.xtext.wizard')
	api project(':org.eclipse.xtext.testlanguages')
	mwe2Implementation project(':org.eclipse.xtext.testlanguages')
	api 'junit:junit'
	api 'org.eclipse.emf:org.eclipse.emf.common'
	api 'org.eclipse.emf:org.eclipse.emf.ecore.xmi'
	mavenOptionalApi files('lib/simple.jar')
	// The MWE2 workflow depends on emf-gen, so we have to include it in the test dependencies
	testImplementation sourceSets.mwe2.output
	testImplementation 'args4j:args4j'
}

sourceSets.test {
	java.srcDirs = ['src', 'src-gen', 'suites']
	if (findProperty('compileXtend') != 'true') {
		java.srcDir 'xtend-gen'
	}
}

sourceSets.mwe2 {
	java.srcDirs = ['generator/src', 'emf-gen']
	if (findProperty('compileXtend') == 'true') {
		xtendOutputDir = 'generator/xtend-gen'
	} else {
		java.srcDir 'generator/xtend-gen'
	}
	runtimeClasspath += processTestResources.outputs.files
}
jar.from sourceSets.mwe2.output
sourcesJar.from sourceSets.mwe2.allSource

task generateTestLanguages(type: XtextGeneratorTask) {
	workflow = file('src/org/eclipse/xtext/GenerateAllTestLanguages.mwe2')
	outputs.dir 'src-gen'
}

test {
	exclude 'org.eclipse.xtext.serializer.contextFinderTest.NestedTypeRecursiveTest.class'
	exclude '**/*Suite.class'
}
